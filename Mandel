*BASIC
NEW
AUTO
REM>Mandel
REM v3.3.2 (2021-11-01)
REM Mandelbrot generator using
REM Tube processor and block
REM screen memory xfer OSWORD
:
MODE 2
:
OSWORD=&FFF1
DISPLAYSTART=&3000
:
DIM params% 20
DIM colourmap%(1,15)
:
REM parameters to adjust
REM generation performance
doublebuf%=TRUE
buflines%=32
maxiters%=80
:
IF (256 MOD buflines%)<>0 THEN PRINT "Error: buflines% must be a divisor of 256":STOP
DIM screenbuf1% 80*buflines%
IF doublebuf% THEN DIM screenbuf2% 80*buflines%
:
REM install TubeXfer OSWORD &E0
REM in host processor and hook
REM USERV if not already
REM installed
:
REM catch if not loaded and try
REM to load it
inerr%=FALSE
ON ERROR IF inerr% THEN REPORT:PRINT " in error at line ";ERL:END ELSE inerr%=TRUE:IF ERR=254 THEN OSCLI("RUN TubeXfrX") ELSE REPORT:PRINT " at line ";ERL:END
REM test if present
params%?0=4
params%?1=3
params%?2=0
params%?3=&FF:REM =test
PROCosword(&E0,params%)
ON ERROR OFF
IF params%?2<>1 THEN PRINT "Failed loading TubeXfer":STOP
:
FOR pixel%=0 TO 1
FOR colour%=0 TO 15
colourmap%(pixel%,colour%)=(2-pixel%)*((colour% AND 1)+2*(colour% AND 2)+4*(colour% AND 4)+8*(colour% AND 8))
NEXT
NEXT
:
REM start with first buffer
screenbuf%=screenbuf1%
IF doublebuf% THEN othscreenbuf%=screenbuf2%
:
REM centre and screen "radius"
rc=-0.5
ic=0
rr=1.65
ir=1.4
:
REM zoom/pan redraw loop
anim%=FALSE
REPEAT
:
start%=TIME
screenblock%=DISPLAYSTART
REM precalculate screen->num
REM scaling factor
rscl=rr/80
iscl=ir/128
:
totaliters%=0
FOR ys%=255 TO 0 STEP -buflines%
byte%=0
FOR y%=ys% TO ys%-buflines%+1 STEP -8
PRINT TAB(0,31);SPC(3-LEN(STR$(y%)));y%;
FOR x%=0 TO 159 STEP 2
FOR yi%=y% TO y%-7 STEP -1
value%=0
FOR offset%=0 TO 1
iters%=FNmandel((x%+offset%-80)*rscl+rc,(yi%-128)*iscl+ic,maxiters%)
totaliters%=totaliters%+iters%
IF iters%<maxiters% THEN value%=value% OR colourmap%(offset%,(iters% MOD 7)+1)
NEXT
screenbuf%?byte%=value%
byte%=byte%+1
NEXT
NEXT
NEXT
PROCxfer2h(screenbuf%,screenblock%,80*buflines%,NOT(doublebuf%))
IF doublebuf% THEN tmpscreenbuf%=screenbuf%:screenbuf%=othscreenbuf%:othscreenbuf%=tmpscreenbuf%
screenblock%=screenblock%+byte%
NEXT
end%=TIME
:
PRINT TAB(0,29);(end%-start%)/100;"s"';totaliters%;" iters";
:
REM shift the displayed area
rc=rc-rr/160
rr=rr/1.005
ir=ir/1.005
:
REM loop if we're animating and
REM we didn't get a screen of
REM nothing (all out of range or
REM all in the set)
UNTIL NOT(anim%) OR totaliters%=160*256 OR totaliters%=160*256*maxiters%
:
REM return cursor to home
VDU 30
END
:
DEFPROCosword(num%,addr%)
LOCAL A%,X%,Y%
A%=num%
X%=addr% MOD 256
Y%=addr% DIV 256
CALL OSWORD
ENDPROC
:
DEFPROCxfer2h(paddr%,haddr%,len%,wait%)
params%?0=12:REM 12 bytes sent
params%?1=wait%*-3:REM 3 bytes returned if waiting
params%?2=0:REM return code
params%?3=0:REM op=0 => P>H
params%!4=paddr%
params%?8=haddr% MOD 256
params%?9=haddr% DIV 256
params%?10=len% MOD 256
params%?11=len% DIV 256
REM OSWORD &E0 is TubeXfr
PROCosword(&E0,params%)
IF wait% AND params%?2<>1 THEN PRINT "Error calling TubeXfer":STOP
ENDPROC
:
DEFFNmandel(x,y,maxiter%)
LOCAL xi,yi,iter%,xj
xi=x
yi=y
iter%=0
REPEAT
xj=xi*xi-yi*yi+x
yi=2*xi*yi+y
xi=xj
iter%=iter%+1
UNTIL iter%>maxiter% OR (xi*xi+yi*yi)>=4
=iter%
