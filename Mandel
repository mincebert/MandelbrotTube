*BASIC
NEW
AUTO
 REM>Mandel
 MODE 2
 DIM params% 100
 DIM colourmap%(1,15)
 DIM data% &5000
 USERV=&200
 :
 start%=TIME
 FOR pixel%=0 TO 1
 FOR colour%=0 TO 15
 colourmap%(pixel%,colour%)=(2-pixel%)*((colour% AND 1)+2*(colour% AND 2)+4*(colour% AND 4)+8*(colour% AND 8))
 NEXT
 NEXT
 :
 D%=32
 byte%=data%
 FOR y%=255 TO 0 STEP -8
 FOR x%=0 TO 159 STEP 2
 FOR yi%=y% TO y%-7 STEP -1
 value%=0
 C%=FNmandel((x%-100)/48,(yi%-128)/96,D%)
 IF C%<>D% THEN value%=colourmap%(0,(C% MOD 7)+1)
 C%=FNmandel((x%+1-100)/48,(yi%-128)/96,D%)
 IF C%<>D% THEN value%=value% OR colourmap%(1,(C% MOD 7)+1)
 ?byte%=value%
 byte%=byte%+1
 NEXT
 NEXT
 NEXT
 PROCxferio(data%,&3000,&5000)
 :
 PRINT ;(TIME-start%)/100;"s";
 END
 :
 DEFPROCwriteio(addr%,value%)
 !params%=addr%
 params%?4=value%
 PROCosword(6,params%)
 ENDPROC
 :
 DEFPROCosword(num%,addr%)
 SYS &7,num%,addr%
 ENDPROC
 :
 DEFPROCxferio(paddr%,ioaddr%,len%)
 LOCAL finish%
 params%?0=11
 params%?1=0
 params%?2=0
 params%!3=paddr%
 params%?7=ioaddr% MOD 256
 params%?8=ioaddr% DIV 256
 params%?9=len% MOD 256
 params%?10=len% DIV 256
 PROCosword(&E0,params%)
 ENDPROC
 :
 DEFFNmandel(x,y,maxiter%)
 LOCAL xi,yi,iter%,xj
 xi=x
 yi=y
 iter%=0
 WHILE iter%<maxiter% AND (xi*xi+yi*yi)<4
 xj=xi*xi-yi*yi+x
 yi=2*xi*yi+y
 xi=xj
 iter%+=1
 ENDWHILE
 =iter%
>*SPOOL
